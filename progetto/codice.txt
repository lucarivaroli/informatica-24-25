#include <Wire.h>                   //comunicazione con display LCD I2C
#include <LiquidCrystal_I2C.h>      //display LCD I2C

#define TRIG 7              //pin di uscita sensore ultrasuoni
#define ECHO 6              //pin di entrata sensore ultrasuoni
#define RELAY 8
#define LED_LUCE 5         // led (rosso) che si accende con poca luce.
#define LED_LAMPEGGIO 4    // led (verde) che lampeggia nel ciclo for.
#define LUMINOSITA_PIN A0   //pin del fotoresitore (luminosita)

LiquidCrystal_I2C lcd(0x27, 20, 4);



void lampeggiaLED(int pin, int lampeggi, int durata) {
    for (int i = 0; i < lampeggi; i++) {
        digitalWrite(pin, HIGH);
        delay(durata);
        digitalWrite(pin, LOW);
        delay(durata);
    }
}

// eseguita una sola volta 
void setup() {
    pinMode(TRIG, OUTPUT);
    pinMode(ECHO, INPUT);
    pinMode(RELAY, OUTPUT);
    pinMode(LED_LUCE, OUTPUT);
    pinMode(LED_LAMPEGGIO, OUTPUT);
    digitalWrite(RELAY, HIGH);          //il relay parte da high (spento)

    Serial.begin(9600);         //inizia la comunicazione

    lcd.init();                 //inizializza il display
    lcd.backlight();            //accende la retroilluminazione
    lcd.setCursor(0, 0);
    lcd.print("Distributore ON");
    delay(1000);
}

long misuraDistanza() {
    digitalWrite(TRIG, LOW);                    //serve per assicurasi che sia in riposo
    delayMicroseconds(2);           
    digitalWrite(TRIG, HIGH);                   //inizio misurazione
    delayMicroseconds(10);
    digitalWrite(TRIG, LOW);                    //chiude l'impulso (fine della misurazione)

    long durata = pulseIn(ECHO, HIGH);          //Misura il tempo che l’impulso impiega a tornare al sensore.
    long distanza = durata * 0.034 / 2;
    return distanza;
}

//eseguita continuamente
void loop() {
    long distanza = misuraDistanza();               // Ottiene la distanza misurata dal sensore
    int luminosita = analogRead(LUMINOSITA_PIN);    // legge la luminosita e la converte in un valore tra 0 a 1023

    //stampa i valori sul monitor
    Serial.print("Distanza: ");
    Serial.print(distanza);
    Serial.print(" cm, Luminosità: ");
    Serial.println(luminosita);

    lcd.clear();                    //pulisce il display
    lcd.setCursor(0, 0);
    lcd.print("Distanza: ");
    lcd.print(distanza);
    lcd.print("cm");

    lcd.setCursor(0, 1);
    if (distanza <= 10) {
        
        lampeggiaLED(LED_LAMPEGGIO, 3, 200);

        digitalWrite(RELAY, LOW);               //accende il relay
        lcd.print("Pompa: ACCESA");
        lcd.setCursor(0, 2);
        lcd.print("Grazie, arrivederci! ");
    } else {
        digitalWrite(RELAY, HIGH);
        lcd.print("Pompa: SPENTA");
        lcd.setCursor(0, 2);
        lcd.print("Metti il bicchiere! ");
    }

    
    if (luminosita < 35) {
        digitalWrite(LED_LUCE, HIGH);
    } else {
        digitalWrite(LED_LUCE, LOW);
    }

    delay(500);
}